apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.syo_sa1982.todo4android"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    // DataBindingを有効化
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // ビルド環境を設定
    flavorDimensions "default"
    productFlavors {
        // 開発環境
        develop {
            applicationIdSuffix '.develop'
            // APIのエンドポイント
            buildConfigField "String", "API_ENDPOINT", '"https://sample.com/"'
            // OkHttp（通信ログ）ライブラリの有効判定
            buildConfigField "Boolean", "OK_HTTP_LOG_ENABLED", 'true'
        }
        // モック環境
        mock {
            applicationIdSuffix '.mock'
            buildConfigField "String", "API_ENDPOINT", '"https://sample.com/"'
            buildConfigField "Boolean", "OK_HTTP_LOG_ENABLED", 'true'
        }
    }
    // ソースディレクトリを指定
    sourceSets {
        // 共通ソース(Javaディレクトリが自動生成されないようにKotlinディレクトリを先に配置)
        main {
            java.srcDirs = ['src/main/kotlin', 'src/main/java']
        }
        // 開発環境ソース
        develop {
            java.srcDirs +='src/develop/kotlin/'
        }
        // テスト環境ソース
        test {
            java.srcDirs += 'src/test/kotlin'
        }
        // テスト(Android)環境ソース
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    kapt "com.android.databinding:compiler:3.1.4"

    // AndroidSupport
    final ANDROID_SUPPORT_VERSION = "27.1.1"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"
    implementation "com.android.support:support-v4:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:design:${ANDROID_SUPPORT_VERSION}"

    // Android Architecture Components
    final ANDROID_ARCHITECTURE_COMPONENTS_VERSION = "1.1.1"
    implementation "android.arch.lifecycle:extensions:${ANDROID_ARCHITECTURE_COMPONENTS_VERSION}"

    // NetWork
    final RETROFIT_VERSION = "2.4.0"
    final OK_HTTP_VERSION = "3.10.0"
    implementation "com.squareup.okhttp3:okhttp:${OK_HTTP_VERSION}"
    implementation "com.squareup.okhttp3:logging-interceptor:${OK_HTTP_VERSION}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-moshi:${RETROFIT_VERSION}"
    implementation("com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}") {
        exclude module: 'okhttp'
    }

    // Json Converter
    final KOTSHI_VERSION = "1.0.4"
    final MOSHI_VERSION = "1.5.0"
    kapt "se.ansman.kotshi:compiler:${KOTSHI_VERSION}"
    implementation "se.ansman.kotshi:api:${KOTSHI_VERSION}"
    implementation "com.squareup.moshi:moshi:${MOSHI_VERSION}"
    implementation "com.squareup.moshi:moshi-adapters:${MOSHI_VERSION}"

    // UI
    implementation "com.ncapdevi:frag-nav:2.4.0"

    // Reactive
    implementation "io.reactivex.rxjava2:rxjava:2.1.12"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxkotlin:2.1.0"

    // Test
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}
