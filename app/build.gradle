apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.syousa1982.todo4android"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

//        def properties = new Properties()
//        properties.load(project.rootProject.file('local.properties').newDataInputStream())
//        // local.propertiesのapi_keyに定義されている値を読み込む
//        def OPEN_TASK_API_KEY = properties.getProperty("TaskApiToken")
//        // buildConfigFieldに定義
//        buildConfigField("String", "OPEN_TASK_API_KEY", OPEN_TASK_API_KEY)
    }
    // DataBindingを有効化
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // ビルド環境を設定
    flavorDimensions "default"
    productFlavors {
        // 開発環境
        develop {
            applicationIdSuffix '.develop'
            // APIのエンドポイント
            buildConfigField "String", "API_ENDPOINT", '"https://6653m7jnhc.execute-api.ap-northeast-1.amazonaws.com/dev/v1/"'
            // OkHttp（通信ログ）ライブラリの有効判定
            buildConfigField "Boolean", "OK_HTTP_LOG_ENABLED", 'true'
        }
        // モック環境
        mock {
            applicationIdSuffix '.mock'
            buildConfigField "String", "API_ENDPOINT", '"https://sample.com/"'
            buildConfigField "Boolean", "OK_HTTP_LOG_ENABLED", 'true'
        }
    }
    // ソースディレクトリを指定
    sourceSets {
        // 共通ソース(Javaディレクトリが自動生成されないようにKotlinディレクトリを先に配置)
        main {
            java.srcDirs = ['src/main/kotlin', 'src/main/java']
        }
        // テスト環境ソース
        test {
            java.srcDirs += 'src/test/kotlin'
        }
        // テスト(Android)環境ソース
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
    }
    // Lintオプションを設定
    // "./gradlew lint"で実行
    lintOptions {
        // Lint設定ファイル
        lintConfig file("lint-config.xml")
        // Lint結果出力
        htmlReport true
        htmlOutput file("$buildDir/reports/lint-results.html")
        abortOnError false
    }
    // Lintオプションを設定
    // "./gradlew test"で実行
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }
}

kapt {
    generateStubs = true
    javacOptions {
        option("-Xmaxerrs", 5000)
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Language
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // AndroidX
    implementation "com.google.android.material:material:1.1.0-alpha04"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.annotation:annotation:1.0.2"
    implementation "androidx.exifinterface:exifinterface:1.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "android.arch.navigation:navigation-fragment:$nav_version" // use -ktx for Kotlin
    implementation "android.arch.navigation:navigation-ui:$nav_version" // use -ktx for Kotlin
    // NetWork
    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    // Converter
    kapt "se.ansman.kotshi:compiler:$kotshi_version"
    implementation "se.ansman.kotshi:api:$kotshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    // DI
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    // UI
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.xwray:groupie:$groupie_version"
    implementation "com.xwray:groupie-databinding:$groupie_version"
    implementation "com.xwray:groupie-kotlin-android-extensions:$groupie_version"
    implementation "com.firebaseui:firebase-ui-firestore:4.1.0"
    implementation "com.ncapdevi:frag-nav:2.4.0"
    implementation "com.otaliastudios:cameraview:1.6.0"
    // Reactive
    implementation "io.reactivex.rxjava2:rxjava:2.2.3"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    // Debug & Log
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    // Test
    testImplementation "junit:junit:4.12"
    testImplementation "com.squareup.okhttp3:mockwebserver:$ok_http_version"
    testImplementation "org.robolectric:robolectric:4.0"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
    testImplementation "org.dbtools:dbtools-room-jdbc:$dbtoolsRoomVersion"
    testImplementation "org.xerial:sqlite-jdbc:3.27.2"

    // spek
    testImplementation('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
    testImplementation 'org.junit.platform:junit-platform-runner:1.1.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "androidx.arch.core:core-testing:2.0.1"

    // spek requires kotlin-reflect, omit when already in classpath
//    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // MockK
    testImplementation("io.mockk:mockk:$mockk_version")
}
// GoogleServicesプラグインを適用(dependenciesよりも下に設定する)
//apply plugin: "com.google.gms.google-services"

