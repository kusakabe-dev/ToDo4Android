apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.cookpad.android.licensetools'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'androidx.navigation.safeargs'

android {
    signingConfigs {
        config {
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file(System.getenv("HOME") + "/keystores/android.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
        }
    }
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.syousa1982.todo4android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 2
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    // DataBindingを有効化
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            resValue "string", "app_name", "ToDo 開発"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            resValue "string", "app_name", "ToDo"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    // ソースディレクトリを指定
    sourceSets {
        // 共通ソース(Javaディレクトリが自動生成されないようにKotlinディレクトリを先に配置)
        main {
            java.srcDirs = ['src/main/kotlin', 'src/main/java']
        }
        // テスト環境ソース
        test {
            java.srcDirs += 'src/test/kotlin'
        }
        // テスト(Android)環境ソース
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
    }
    // Lintオプションを設定
    // "./gradlew lint"で実行
    lintOptions {
        // Lint設定ファイル
        lintConfig file("lint-config.xml")
        // Lint結果出力
        htmlReport true
        htmlOutput file("$buildDir/reports/lint-results.html")
        abortOnError false
    }
    // Lintオプションを設定
    // "./gradlew test"で実行
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }
}

kapt {
    generateStubs = true
    javacOptions {
        option("-Xmaxerrs", 5000)
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}


dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    // Serialization
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    implementation "android.arch.navigation:navigation-fragment:$nav_version" // use -ktx for Kotlin
    implementation "android.arch.navigation:navigation-ui:$nav_version" // use -ktx for Kotlin
    // DI
    // Koin AndroidX Scope feature
    implementation "org.koin:koin-androidx-scope:$koin_version"
    // Koin AndroidX ViewModel feature
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    // Koin AndroidX Fragment Factory (unstable version)
    implementation "org.koin:koin-androidx-fragment:$koin_version"

    // UI
    implementation 'com.xwray:groupie:2.7.2'
    implementation 'com.xwray:groupie-databinding:2.7.2'
    implementation 'com.github.bumptech.glide:glide:4.10.0'

    // Test
    testImplementation "org.robolectric:robolectric:4.2.1"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
    testImplementation "org.dbtools:dbtools-room-jdbc:$dbtoolsRoomVersion"
    testImplementation "org.xerial:sqlite-jdbc:3.27.2"

    // spek
    testImplementation 'org.junit.platform:junit-platform-runner:1.1.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    // MockK
    testImplementation("io.mockk:mockk:$mockk_version")
}
// GoogleServicesプラグインを適用(dependenciesよりも下に設定する)
//apply plugin: "com.google.gms.google-services"

